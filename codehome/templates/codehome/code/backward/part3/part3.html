{% load static %}

<h1 id="clanguageseg.1">C Language Seg.1</h1>

<h2 id="tableofcontents">Table of Contents</h2>

<ul>
<li><a href="#callingconvensionforx86">Calling Convension for x86</a>

<ul>
<li><a href="#cdecl">CDECL</a></li>
<li><a href="#stdcallandfastcall">STDCALL and FASTCALL</a></li>
</ul></li>
<li><a href="#nakedfunction">Naked Function</a></li>
<li><a href="#datatype">Data Type</a></li>
<li><a href="#memorylayer">Memory Layer</a>

<ul>
<li><a href="#argumentsjudgement">Arguments Judgement</a></li>
</ul></li>
<li><a href="#ifclause">IF Clause</a>

<ul>
<li><a href="#ifthen">If Then</a></li>
<li><a href="#ifelse">If Else</a></li>
<li><a href="#ifelseif">If Else If</a></li>
</ul></li>
</ul>

<p><TableEndMark></p>

<section	id="callingconvensionforx86">
<h2>Calling Convension for x86</h2>

<p><strong>Calling conventions</strong> describe the interface of called code:</p>

<ul>
<li>The order in which atomic (scalar) parameters, or individual parts of a complex parameter, are allocated.</li>
<li>How parameters are passed (pushed on the stack, placed in registers, or a mix of both).</li>
<li>Which registers the called function must preserve for the caller (also known as: callee-saved registers or non-volatile registers).</li>
<li>How the task of preparing the stack for, and restoring after, a function call is divided between the caller and the callee.</li>
</ul>

<p>Calling conventions, type representations, and name mangling are all part of what is known as an application binary interface (ABI).</p>

<section	id="cdecl">
<h3>CDECL</h3>

<p>The <strong>cdecl (which stands for C declaration)</strong> is a calling convention that originates from Microsoft&#8217;s compiler for the C programming language and is used by many C compilers for the x86 architecture. In cdecl, subroutine arguments are passed on the stack. Integer values and memory addresses are returned in the EAX register. The cdecl calling convention is usually the default calling convention for x86 C compilers, although many compilers provide options to automatically change the calling conventions used.</p>

<ul>
<li>In the context of the C programming language, function arguments are <em>pushed on the stack</em> in the <em>right-to-left</em> order, i.e. the last argument is pushed first.</li>
<li>Stack balance is the responsibility of caller, hence adecl is count as a member of <code>caller clean-up convention</code>.</li>
</ul>
<pre><code class="C">#include &quot;stdio.h&quot;

// same as `int __cdecl callee(int x, int y, int z)`
int callee(int x, int y, int z) {
	return x + y + z;
}

// same as `int __cdecl caller()`
int caller() {
	return callee(1, 2, 3) + 5;
}

void main(int argc, char* argv[]) {
	
	caller();

	return 0;
}</code></pre>
<pre><code class="C">// for caller
7:    int caller() {
00401070 55                   push        ebp
00401071 8B EC                mov         ebp,esp
00401073 83 EC 40             sub         esp,40h
00401076 53                   push        ebx
00401077 56                   push        esi
00401078 57                   push        edi
00401079 8D 7D C0             lea         edi,[ebp-40h]
0040107C B9 10 00 00 00       mov         ecx,10h
00401081 B8 CC CC CC CC       mov         eax,0CCCCCCCCh
00401086 F3 AB                rep stos    dword ptr [edi]
8:        return callee(1, 2, 3) + 5;
00401088 6A 03                push        3
0040108A 6A 02                push        2
0040108C 6A 01                push        1
0040108E E8 72 FF FF FF       call        @ILT+0(_callee) (00401005)
00401093 83 C4 0C             add         esp,0Ch
00401096 83 C0 05             add         eax,5
9:    }
00401099 5F                   pop         edi
0040109A 5E                   pop         esi
0040109B 5B                   pop         ebx
0040109C 83 C4 40             add         esp,40h
0040109F 3B EC                cmp         ebp,esp
004010A1 E8 5A 00 00 00       call        __chkesp (00401100)
004010A6 8B E5                mov         esp,ebp
004010A8 5D                   pop         ebp
004010A9 C3                   ret</code></pre>
<pre><code class="C">// for callee
3:    int callee(int x, int y, int z) {
00401030 55                   push        ebp
00401031 8B EC                mov         ebp,esp
00401033 83 EC 40             sub         esp,40h
00401036 53                   push        ebx
00401037 56                   push        esi
00401038 57                   push        edi
00401039 8D 7D C0             lea         edi,[ebp-40h]
0040103C B9 10 00 00 00       mov         ecx,10h
00401041 B8 CC CC CC CC       mov         eax,0CCCCCCCCh
00401046 F3 AB                rep stos    dword ptr [edi]
4:        return x + y + z;
00401048 8B 45 08             mov         eax,dword ptr [ebp+8]
0040104B 03 45 0C             add         eax,dword ptr [ebp+0Ch]
0040104E 03 45 10             add         eax,dword ptr [ebp+10h]
5:    }
00401051 5F                   pop         edi
00401052 5E                   pop         esi
00401053 5B                   pop         ebx
00401054 8B E5                mov         esp,ebp
00401056 5D                   pop         ebp
00401057 C3                   ret</code></pre>

</section>
<section	id="stdcallandfastcall">
<h3>STDCALL and FASTCALL</h3>

<p><strong>The stdcall calling convention</strong> is a variation on the Pascal calling convention in which the callee is responsible for cleaning up the stack, but the parameters are pushed onto the stack in right-to-left order, as in the _cdecl calling convention. Registers EAX, ECX, and EDX are designated for use within the function. Return values are stored in the EAX register.</p>

<p>In the example, only part of critical segement will be presented.</p>
<pre><code class="C">#include &quot;stdio.h&quot;

int __stdcall callee(int x, int y, int z) {
	return x + y + z;
}

int __stdcall caller() {
	return callee(1, 2, 3) + 5;
}

void main(int argc, char* argv[]) {
	
	caller();

	return 0;
}</code></pre>
<pre><code class="C">8:        return callee(1, 2, 3) + 5;
00401088 6A 03                push        3
0040108A 6A 02                push        2
0040108C 6A 01                push        1
// callee is responsible for balance stack
0040108E E8 86 FF FF FF       call        @ILT+20(_callee) (00401019)
00401093 83 C0 05             add         eax,5</code></pre>
<pre><code class="C">3:    int __stdcall callee(int x, int y, int z) {
00401030 55                   push        ebp
00401031 8B EC                mov         ebp,esp
00401033 83 EC 40             sub         esp,40h
00401036 53                   push        ebx
00401037 56                   push        esi
00401038 57                   push        edi
00401039 8D 7D C0             lea         edi,[ebp-40h]
0040103C B9 10 00 00 00       mov         ecx,10h
00401041 B8 CC CC CC CC       mov         eax,0CCCCCCCCh
00401046 F3 AB                rep stos    dword ptr [edi]
4:        return x + y + z;
00401048 8B 45 08             mov         eax,dword ptr [ebp+8]
0040104B 03 45 0C             add         eax,dword ptr [ebp+0Ch]
0040104E 03 45 10             add         eax,dword ptr [ebp+10h]
5:    }
00401051 5F                   pop         edi
00401052 5E                   pop         esi
00401053 5B                   pop         ebx
00401054 8B E5                mov         esp,ebp
00401056 5D                   pop         ebp
00401057 C2 0C 00             ret         0Ch  // important!!!</code></pre>

<p>Microsoft <strong><strong>fastcall convention (aka </strong>msfastcall)</strong> passes the first two arguments (evaluated left to right) that fit into ECX and EDX. Remaining arguments are pushed onto the stack from right to left. Also, callee takes task of balancing the stack. </p>
<pre><code class="C">#include &quot;stdio.h&quot;

int __fastcall callee(int x, int y, int z) {
	return x + y + z;
}

int __fastcall caller() {
	return callee(1, 2, 3) + 5;
}

void main(int argc, char* argv[]) {
	
	caller();

	return 0;
}</code></pre>
<pre><code class="C">8:        return callee(1, 2, 3) + 5;
// notice that the two arguments left most will be stored into registers edx and ecx respectively.
0040108A 6A 03                push        3
0040108C BA 02 00 00 00       mov         edx,2
00401091 B9 01 00 00 00       mov         ecx,1
00401096 E8 88 FF FF FF       call        @ILT+30(_callee@12) (00401023)
0040109B 83 C0 05             add         eax,5</code></pre>
<pre><code class="C">3:    int __fastcall callee(int x, int y, int z) {
00401030 55                   push        ebp
00401031 8B EC                mov         ebp,esp
00401033 83 EC 48             sub         esp,48h
00401036 53                   push        ebx
00401037 56                   push        esi
00401038 57                   push        edi
00401039 51                   push        ecx
0040103A 8D 7D B8             lea         edi,[ebp-48h]
0040103D B9 12 00 00 00       mov         ecx,12h
00401042 B8 CC CC CC CC       mov         eax,0CCCCCCCCh
00401047 F3 AB                rep stos    dword ptr [edi]
00401049 59                   pop         ecx
0040104A 89 55 F8             mov         dword ptr [ebp-8],edx
0040104D 89 4D FC             mov         dword ptr [ebp-4],ecx
4:        return x + y + z;
00401050 8B 45 FC             mov         eax,dword ptr [ebp-4]
00401053 03 45 F8             add         eax,dword ptr [ebp-8]
00401056 03 45 08             add         eax,dword ptr [ebp+8]
5:    }
00401059 5F                   pop         edi
0040105A 5E                   pop         esi
0040105B 5B                   pop         ebx
0040105C 8B E5                mov         esp,ebp
0040105E 5D                   pop         ebp
0040105F C2 04 00             ret         4</code></pre>

</section>
</section>
<section	id="nakedfunction">
<h2>Naked Function</h2>

<p>For functions declared with the <code>naked</code> attribute, the compiler generates code without prolog and epilog code. You can use this feature to write your own prolog/epilog code sequences using inline assembler code. </p>
<pre><code class="C">// declspec stands for declare special
void __declspec(naked) func() {

}

// if programmer want to write assembly language in C, this is a desired feature
void __declspec(naked) func() {
    __asm {
        push ebp
        mov ebp,esp
        sub esp,0x40
        push ebx
        push esi
        push edi
        lea edi,dword ptr ds:[ebp-0x40]
        mov eax,CCCCCCCC
        mov ecx,0x10
        rep stosd
        // rep stos dword ptr es:[edi]

        pop edi
        pop esi
        pop ebx
        mov esp,ebp
        pop ebp
        
        ret
    }
}</code></pre>

</section>
<section	id="datatype">
<h2>Data Type</h2>

<p>There are four basic data type in C &#8211; <code>char</code>, <code>short</code>, <code>int</code> and <code>long</code>, each of them has various data width in memory.</p>

<table>
<colgroup>
<col style="text-align:center;"/>
<col style="text-align:center;"/>
<col style="text-align:center;"/>
</colgroup>

<thead>
<tr>
	<th style="text-align:center;">Data Type</th>
	<th style="text-align:center;">Bits</th>
	<th style="text-align:center;">Bytes</th>
</tr>
</thead>

<tbody>
<tr>
	<td style="text-align:center;">char</td>
	<td style="text-align:center;">8 Bits</td>
	<td style="text-align:center;">1 Byte</td>
</tr>
<tr>
	<td style="text-align:center;">short</td>
	<td style="text-align:center;">16 Bits</td>
	<td style="text-align:center;">2 Bytes</td>
</tr>
<tr>
	<td style="text-align:center;">int</td>
	<td style="text-align:center;">32 Bits</td>
	<td style="text-align:center;">4 Bytes</td>
</tr>
<tr>
	<td style="text-align:center;">long</td>
	<td style="text-align:center;">32 Bits</td>
	<td style="text-align:center;">4 Bytes</td>
</tr>
</tbody>
</table>

<p>There are two basic data category, signed or unsigned. In default, <code>char</code>, <code>short</code>, <code>int</code> and <code>long</code> mean <code>signed char</code>, <code>signed short</code>, <code>signed int</code>, <code>signed long</code>. For example, for an 8-bit-width data, the computer will interpret it as totally two value. </p>
<pre><code class="C">#include &quot;stdio.h&quot;

int main(int argc, char* argv[]) {

    char ch = 0xFF;
    unsigned uch = 0xFF;

    printf(&quot;%d \n %d&quot;, ch, uch);

    return 0;
}</code></pre>
<pre><code class="bash">Console Output:
-1
255</code></pre>

<p>That&#8217;s why we, as an instructor of a computer, have to be careful and responsible for how the data is interpreted with <strong>TYPE CONTROL</strong>.</p>

<p>Let&#8217;s discuss it from assembly perspective.</p>
<pre><code class="C">#include &quot;stdio.h&quot;

void myCompareSigned(int x, int y) {
    if (x &lt; y) {
        printf(&quot;x &lt; y&quot;);
    }
    else {
        printf(&quot;x &gt; y&quot;);
    }
}

void myCompareUnsigned(unsigned int x, unsigned int y) {
    if (x &lt; y) {
        printf(&quot;x &lt; y&quot;);
    }
    else {
        printf(&quot;x &gt; y&quot;);
    }
}

int main(int argc, char* argv[]) {

    myCompareSigned(1, 0xFF);
    myCompareUnsigned(1, 0xFF);

    return 0;
}</code></pre>
<pre><code class="C">// Although the data are interpreted as different thing, they are stored in same form.
23:       myCompareSigned(1, 0xFF);
00401108 68 FF 00 00 00       push        0FFh
0040110D 6A 01                push        1
0040110F E8 FB FE FF FF       call        @ILT+10(_myCompareSigned) (0040100f)
00401114 83 C4 08             add         esp,8

24:       myCompareUnsigned(1, 0xFF);
00401117 68 FF 00 00 00       push        0FFh
0040111C 6A 01                push        1
0040111E E8 E7 FE FF FF       call        @ILT+5(_myCompareUnsigned) (0040100a)
00401123 83 C4 08             add         esp,8</code></pre>
<pre><code class="C">// for myCompareSigned, using signed instuction jge(jump if greater and equal), remember that greater or less for signed number!
4:        if (x &lt; y) {
00401048 8B 45 08             mov         eax,dword ptr [ebp+8]
0040104B 3B 45 0C             cmp         eax,dword ptr [ebp+0Ch]
0040104E 7D 0F                jge         myCompareSigned+2Fh (0040105f)</code></pre>
<pre><code class="C">// for myComparedUnsigned, using unsigned instruction jae(jump if above and equal), remember that above and below for unsigned!
13:       if (x &lt; y) {
004010A8 8B 45 08             mov         eax,dword ptr [ebp+8]
004010AB 3B 45 0C             cmp         eax,dword ptr [ebp+0Ch]
004010AE 73 0F                jae         myCompareUnsigned+2Fh (004010bf)</code></pre>

</section>
<section	id="memorylayer">
<h2>Memory Layer</h2>

<p>Memory is divided into 5 parts roughly &#8211; code part, stack, heap, global variable part, and constant part.</p>
<pre><code class="bash">        |----------|
        |   Code   |    # &lt;&lt;===  readable and executable.
        |----------| 
        |          |    # &lt;&lt;===  readable and writable,
        |  Stack   |    # for temporary var, arguments
        |          |    # and buffer data
        |----------|
        |          |    
        |   Heap   |    # &lt;&lt;===  readable and writable, 
        |          |    # for dynamic allocation.
        |----------|
        |  Global  |    # &lt;&lt;===  readable and writable.
        | Variable |
        |----------|
        | Constant |    # &lt;&lt;===  read only.
        |----------|
</code></pre>

<ul>
<li>Global variables: are featured by a fixed address in code part, like <code>MOV reg, dword ptr ds:[0x0122ff7c]</code>, which is often called <code>base address</code>.</li>
<li>Temporary variables: are stored into stack and discarded when the stack memory has been reclaimed, aka the calling is finished. Often, their addresses is <code>[ebp-4]</code>, <code>[ebp-8]</code> or <code>ebp-c</code> and so forth.</li>
</ul>

<section	id="argumentsjudgement">
<h3>Arguments Judgement</h3>

<p>Here is an IMPORTANT question &#8211; how to figure out how many arguments a function need by checking a segment of assembly code?</p>
<pre><code class="c">// how many arguments in the function below?
00401050   push        ebp			
00401051   mov         ebp,esp			
00401053   sub         esp,48h		
00401056   push        ebx			
00401057   push        esi			
00401058   push        edi			
00401059   push        ecx    // backup ecx
0040105A   lea         edi,[ebp-48h]			
0040105D   mov         ecx,12h			
00401062   mov         eax,0CCCCCCCCh			
00401067   rep stos    dword ptr [edi]			
00401069   pop         ecx    // restore ecx
0040106A   mov         dword ptr [ebp-8],edx   // arg1		
0040106D   mov         dword ptr [ebp-4],ecx   // arg2			
00401070   mov         eax,dword ptr [ebp-4]			
00401073   add         eax,dword ptr [ebp-8]			
00401076   add         eax,dword ptr [ebp+8]   // arg3			
00401079   mov         [g_x (00427958)],eax	   // here is a global variable	
0040107E   pop         edi			
0040107F   pop         esi			
00401080   pop         ebx			
00401081   mov         esp,ebp			
00401083   pop         ebp			
00401084   ret         4			</code></pre>

<p>There are some tips of how to find arguments.</p>

<ul>
<li>find those suspicious registers, which are used for transfer value. To be specific, we did not assign value to <code>ecx</code> in the function code, however <code>ecx</code> give its value to other <code>reg/memory</code>. for example, <code>dword ptr [ebp-8],edx</code>.</li>
<li>inner balance(<code>ret 4</code>) or outer balance(<code>add ebp, 4</code>).</li>
<li>the # of arguments = # of registers used for transfer value + # of balance</li>
</ul>

</section>
</section>
<section	id="ifclause">
<h2>IF Clause</h2>

<section	id="ifthen">
<h3>If Then</h3>

<p>Let&#8217;s start with C code and analyze its assembly code.</p>
<pre><code class="c">#include &quot;stdio.h&quot;

int z = 0;

void func1(int x, int y) {
	if (x &gt; y) {
		z = x;
	}
}

int main(int argc, char* argv[]) {
	
	func1(1, 2);

	return 0;
}</code></pre>
<pre><code class="c">// stack preparation and reclaim are omitted.
6:        if (x &gt; y) {
00401038 8B 45 08             mov         eax,dword ptr [ebp+8]
0040103B 3B 45 0C             cmp         eax,dword ptr [ebp+0Ch]
0040103E 7E 09                jle         func1+29h (00401049)
7:            z = x;
00401040 8B 4D 08             mov         ecx,dword ptr [ebp+8]
00401043 89 0D 50 7C 42 00    mov         dword ptr [_z (00427c50)],ecx
8:        }
00401049 5F                   pop         edi</code></pre>

<p>Two things worth being discussed are how <code>if</code> works and how global variable is assigned.</p>

<ul>
<li>For a simple <code>if-then</code> clause, the system will affect flag registers by <code>cmp</code> and then use <code>jcc</code> to achieve controlling flow.</li>
<li>The global variable CANNOT be assigned value by a word of memory directly. The process must be done with the help of a register, here is <code>ecx</code>.</li>
</ul>

<p><em>BTW, the condition in assembly is always opposite to that in C since the logic philosophy of assembly is different.</em></p>

</section>
<section	id="ifelse">
<h3>If Else</h3>

<p>Now we have made some change to <code>func1</code> as follow.</p>
<pre><code class="c">// only changed part is shown with respect to example above.
void func1(int x, int y) {
	if (x &gt; y) {
		z = x;
	}
	else {
		z = y;
	}
}</code></pre>

<p>What&#8217;s the difference in assembly code?</p>
<pre><code class="c">6:        if (x &gt; y) {
00401038 8B 45 08             mov         eax,dword ptr [ebp+8]
0040103B 3B 45 0C             cmp         eax,dword ptr [ebp+0Ch]
0040103E 7E 0B                jle         func1+2Bh (0040104b)
7:            z = x;
00401040 8B 4D 08             mov         ecx,dword ptr [ebp+8]
00401043 89 0D 50 7C 42 00    mov         dword ptr [_z (00427c50)],ecx
8:        }
9:        else {
00401049 EB 09                jmp         func1+34h (00401054)
10:           z = y;
0040104B 8B 55 0C             mov         edx,dword ptr [ebp+0Ch]
0040104E 89 15 50 7C 42 00    mov         dword ptr [_z (00427c50)],edx
11:       }
14:   }
00401054 5F                   pop         edi
00401055 5E                   pop         esi
...</code></pre>

<p>Two <code>jcc</code> command are critical here. Notice that the philosophy of assembly language is to execute in sequential order, thereby its idea is to exclude other case by <code>jcc</code>, sorts of like a rule-out protocol. that explains why the condition in assembly code is always opposite to that in C.</p>

<p>At code address <code>00401049</code>, <code>jmp</code> jumps to the instruction to reclaim the stack since one case has done. Therefore, we could conclude that as follow,</p>
<pre><code class="c">// if-else assembly conclusion
cmp var1, var2       // to affect eflag
jcc addr[case2]      // be always opposite to condition in C

....                 //-|
....                 // |-- case1
jmp addr[End]    //-|   BIG DIFFERENCE: jmp yes!

....                 //-|   &lt;&lt;--- the base address of case2
....                 // |-- case2, or else
....                 //-|

pop edi              //     &lt;&lt;--- the base address of End
....

// however in if-then, only 1 case
cmp var1, var2
jcc addr[End]
....                 //-|
....                 // |-- case
....                 //-|
                     //  BIG DIFFERENCE: no jmp!!! 
....                 //  &lt;&lt;--- the base of End address</code></pre>

<p>Given knowledge above, let&#8217;s try to analyse a segment of assembly code, the aim is to find how many global vars, temporary vars there are and if the function return any value, and what the code should be in C.</p>
<pre><code class="c">0040D4A1 8B EC                mov         ebp,esp
0040D4A3 83 EC 48             sub         esp,48h
0040D4A6 53                   push        ebx
0040D4A7 56                   push        esi
0040D4A8 57                   push        edi
0040D4A9 8D 7D B8             lea         edi,[ebp-48h]
0040D4AC B9 12 00 00 00       mov         ecx,12h
0040D4B1 B8 CC CC CC CC       mov         eax,0CCCCCCCCh
0040D4B6 F3 AB                rep stos    dword ptr [edi]
0040D4B8 A1 50 7C 42 00       mov         eax,[_z (00427c50)]
0040D4BD 89 45 FC             mov         dword ptr [ebp-4],eax
0040D4C0 C7 45 F8 02 00 00 00 mov         dword ptr [ebp-8],2
0040D4C7 8B 4D 08             mov         ecx,dword ptr [ebp+8]
0040D4CA 3B 4D 0C             cmp         ecx,dword ptr [ebp+0Ch]
0040D4CD 7C 09                jl          func1+38h (0040d4d8)
0040D4CF 8B 55 F8             mov         edx,dword ptr [ebp-8]
0040D4D2 83 C2 01             add         edx,1
0040D4D5 89 55 F8             mov         dword ptr [ebp-8],edx
0040D4D8 8B 45 08             mov         eax,dword ptr [ebp+8]
0040D4DB 3B 45 0C             cmp         eax,dword ptr [ebp+0Ch]
0040D4DE 7D 0B                jge         func1+4Bh (0040d4eb)
0040D4E0 8B 4D F8             mov         ecx,dword ptr [ebp-8]
0040D4E3 89 0D 50 7C 42 00    mov         dword ptr [_z (00427c50)],ecx
0040D4E9 EB 0C                jmp         func1+57h (0040d4f7)
0040D4EB 8B 55 FC             mov         edx,dword ptr [ebp-4]
0040D4EE 03 55 F8             add         edx,dword ptr [ebp-8]
0040D4F1 89 15 50 7C 42 00    mov         dword ptr [_z (00427c50)],edx
0040D4F7 5F                   pop         edi
0040D4F8 5E                   pop         esi
0040D4F9 5B                   pop         ebx
0040D4FA 8B E5                mov         esp,ebp
0040D4FC 5D                   pop         ebp
0040D4FD C3                   ret</code></pre>

<p>the answer is here,</p>
<pre><code class="c">0040D4A1 8B EC                mov         ebp,esp
0040D4A3 83 EC 48             sub         esp,48h
0040D4A6 53                   push        ebx
0040D4A7 56                   push        esi
0040D4A8 57                   push        edi
0040D4A9 8D 7D B8             lea         edi,[ebp-48h]
0040D4AC B9 12 00 00 00       mov         ecx,12h
0040D4B1 B8 CC CC CC CC       mov         eax,0CCCCCCCCh
0040D4B6 F3 AB                rep stos    dword ptr [edi]
6:        int tmp1 = z;
0040D4B8 A1 50 7C 42 00       mov         eax,[_z (00427c50)]
0040D4BD 89 45 FC             mov         dword ptr [ebp-4],eax
7:        int tmp2 = 2;
0040D4C0 C7 45 F8 02 00 00 00 mov         dword ptr [ebp-8],2
8:
9:        if (x &gt;= y) {
0040D4C7 8B 4D 08             mov         ecx,dword ptr [ebp+8]
0040D4CA 3B 4D 0C             cmp         ecx,dword ptr [ebp+0Ch]
0040D4CD 7C 09                jl          func1+38h (0040d4d8)
10:           tmp2 = tmp2 + 1;
0040D4CF 8B 55 F8             mov         edx,dword ptr [ebp-8]
0040D4D2 83 C2 01             add         edx,1
0040D4D5 89 55 F8             mov         dword ptr [ebp-8],edx
11:       }
12:
13:       if (x &lt; y) {
0040D4D8 8B 45 08             mov         eax,dword ptr [ebp+8]
0040D4DB 3B 45 0C             cmp         eax,dword ptr [ebp+0Ch]
0040D4DE 7D 0B                jge         func1+4Bh (0040d4eb)
14:           z = tmp2;
0040D4E0 8B 4D F8             mov         ecx,dword ptr [ebp-8]
0040D4E3 89 0D 50 7C 42 00    mov         dword ptr [_z (00427c50)],ecx
15:       }
16:       else {
0040D4E9 EB 0C                jmp         func1+57h (0040d4f7)
17:           z = tmp1 + tmp2;
0040D4EB 8B 55 FC             mov         edx,dword ptr [ebp-4]
0040D4EE 03 55 F8             add         edx,dword ptr [ebp-8]
0040D4F1 89 15 50 7C 42 00    mov         dword ptr [_z (00427c50)],edx
18:       }
19:   }
0040D4F7 5F                   pop         edi
0040D4F8 5E                   pop         esi
0040D4F9 5B                   pop         ebx
0040D4FA 8B E5                mov         esp,ebp
0040D4FC 5D                   pop         ebp
0040D4FD C3                   ret</code></pre>

</section>
<section	id="ifelseif">
<h3>If Else If</h3>

<p>Similarly we have made some change to <code>func1</code> as follow.</p>
<pre><code class="c">// only changed part is shown with respect to example above.
void func1(int x, int y, int z) {
	if (x &gt; y) {
		r = x;
	}
	else if (y &gt; z) {
		r = y;
	}
    else if (z &gt; x) {
        r = z;
    }
    else {
        r = 1;
    }
}</code></pre>

<p>Let&#8217;s figure out what&#8217;s the feature of multi-if-else.</p>
<pre><code class="c">...
6:        if (x &gt; y) {
0040D4B8 8B 45 08             mov         eax,dword ptr [ebp+8]
0040D4BB 3B 45 0C             cmp         eax,dword ptr [ebp+0Ch]
0040D4BE 7E 0B                jle         func1+2Bh (0040d4cb)
7:            r = x;
0040D4C0 8B 4D 08             mov         ecx,dword ptr [ebp+8]
0040D4C3 89 0D 50 7C 42 00    mov         dword ptr [_r (00427c50)],ecx
8:        }
9:        else if (y &gt; z) {
0040D4C9 EB 2F                jmp         func1+5Ah (0040d4fa)
0040D4CB 8B 55 0C             mov         edx,dword ptr [ebp+0Ch]
0040D4CE 3B 55 10             cmp         edx,dword ptr [ebp+10h]
0040D4D1 7E 0A                jle         func1+3Dh (0040d4dd)
10:           r = y;
0040D4D3 8B 45 0C             mov         eax,dword ptr [ebp+0Ch]
0040D4D6 A3 50 7C 42 00       mov         [_r (00427c50)],eax
11:       }
12:       else if (z &gt; x) {
0040D4DB EB 1D                jmp         func1+5Ah (0040d4fa)
0040D4DD 8B 4D 10             mov         ecx,dword ptr [ebp+10h]
0040D4E0 3B 4D 08             cmp         ecx,dword ptr [ebp+8]
0040D4E3 7E 0B                jle         func1+50h (0040d4f0)
13:           r = z;
0040D4E5 8B 55 10             mov         edx,dword ptr [ebp+10h]
0040D4E8 89 15 50 7C 42 00    mov         dword ptr [_r (00427c50)],edx
14:       }
15:       else {
0040D4EE EB 0A                jmp         func1+5Ah (0040d4fa)
16:           r = 1;
0040D4F0 C7 05 50 7C 42 00 01 mov         dword ptr [_r (00427c50)],1
17:       }
18:   }
0040D4FA 5F                   pop         edi
...</code></pre>

<p>Notice that multi-if-else is easy to find when we have mastered if-then and if-else. There are some features as follow,</p>

<ul>
<li>a COMMON <code>End</code>, <code>jmp addr[END]</code>, appears in all <code>else-if</code> cases, since only one of all cases will happen.</li>
<li>at beginning of a case, there always be a new <code>cmp</code> and <code>jcc</code> instruction which is mirror to <code>else-if</code>.</li>
<li>in the last case, aka <code>else</code>, there is no <code>cmp</code> or <code>jcc</code>, which is same as <code>else</code> in <code>if-else</code>.</li>
</ul>
<pre><code class="c">// if-else-if assembly conclusion
cmp var1, var2       // to affect eflag
jcc addr[case2]      // be always opposite to condition in C

....                 //-|
....                 // |-- case1
jmp addr[End]        //-|   BIG DIFFERENCE: jmp yes!

cmp var1, var2       //-|   &lt;&lt;--- the base address of case2
jcc addr[case3]      // |-- case2
....                 // |
jmp addr[End]        //-|

cmp var1, var2       //-|   &lt;&lt;--- the base address of case3
jcc addr[case4]      // |-- case3
....                 // |
jmp addr[End]        //-|

....
....

cmp var1, var2       //-|   &lt;&lt;--- the base address of caseN-1
jcc addr[caseN]      // |-- caseN-1
....                 // |
jmp addr[End]        //-|

....                 //-|   &lt;&lt;--- the base address of caseN
....                 // |-- caseN, or else
....                 //-|

pop edi              //     &lt;&lt;--- the base address of End
....
</code></pre>

</section>
</section>
<p><EndMarkdown></p>
 
         <!-- pre and next --> 
         <div class="row"> 
             <div class="col-md-12 prenextstyle"> 
                 <ul class="pagination justify-content-center"> 
                     <li><a href="{% url 'notepage' 'backward' 'part2' %}" class="button primary icon solid fa-backward">Back: 2. Stack Analysis</a></li> 
                     <li><span class="button">3. C Language Seg.1</span></li> 
                     <li><a href="{% url 'notepage' 'backward' 'part4' %}" class="button primary icon solid fa-forward">Next: 4. C Language Seg.2</a></li> 
                 </ul> 
             </div> 
         </div>
